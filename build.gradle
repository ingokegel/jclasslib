apply plugin: 'idea'

version = '5.2.1'
ext.mediaDir = file('media')

buildDir = file('build/gradle')
ext.externalLibsDir = file("$buildDir/externalLibs")

buildscript {scriptHandler ->
    ext.kotlinVersion = '1.2.0'

    repositories {
        flatDir {
            dirs 'lib-compile'
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
    apply from: file('repositories.gradle'), to: scriptHandler

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // for local dokka in lib-compile
        //classpath ':dokka-fatjar'
        //classpath ':dokka-gradle-plugin'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.15'
        classpath "com.install4j:install4j-gradle:7.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {

    group = 'org.gjt.jclasslib'
    buildDir = new File(rootProject.buildDir, path.substring(1).replaceAll(':', '/'))
    version = rootProject.version

    repositories {
        flatDir {
            dirs file('lib'), file("$rootDir/lib-compile")
        }
    }
    apply from: file("$rootDir/repositories.gradle")

    plugins.withType(JavaPlugin).whenPluginAdded {
        dependencies {
            testCompile 'org.testng:testng:6.8.8'
        }

        tasks.withType(JavaCompile).each {JavaCompile compileJava ->
            compileJava.with {
                sourceCompatibility = '1.8'
                targetCompatibility = '1.8'
            }
        }

        tasks.withType(Test).each {Test test ->
            test.with {
                useTestNG()
            }
        }

    }
    
    plugins.withId("kotlin") {
        tasks.getByName("compileKotlin").kotlinOptions{
            languageVersion = "1.2"
            apiVersion = "1.2"
        }
    }
}

task clean {
    doLast {
        delete externalLibsDir
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
    distributionType = Wrapper.DistributionType.ALL
}

task test()
task dist()

gradle.projectsEvaluated {
    getTasksByName('clean', true).each {task ->
        if (task != clean) {
            clean.dependsOn(task)
        }
    }
    getTasksByName('dist', true).each {task ->
        if (task != dist) {
            dist.dependsOn(task)
        }
    }
    getTasksByName('test', true).each {task ->
        if (task != test) {
            test.dependsOn(task)
        }
    }
}

idea {
    module {
        name = 'root'
        excludeDirs += files('build', 'dist', 'media')
    }
}